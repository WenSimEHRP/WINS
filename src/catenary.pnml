switch(FEAT_STATIONS, SELF, _sw_catenary, track_type){
    _AAE: 1;
    NCAE: 1;
    SGAE: 1;
    NBAE: 1;
    SFAE: 1;
    NAAE: 1;
    SDAE: 1;
    NCAA: 1;
    SEAE: 1;
    NBAA: 1;
    NCAD: 1;
    NAAA: 1;
    NBAD: 1;
    NAAD: 1;
    SBAI: 1;
    sCAD: 1;
    SAAI: 1;
    SCAD: 1;
    sBAD: 1;
    SCAE: 1;
    sAAD: 1;
    SBAE: 1;
    SBAD: 1;
    dAAE: 1;
    NHAE: 1;
    ELRL: 1;
    rAA3: 1;
    SAAD: 1;
    dAAN: 1;
    NGAE: 1;
    dBAE: 1;
    NFAE: 1;
    NEAE: 1;
    SAAA: 1;
    SCAA: 1;
    SBAA: 1;
    SHAA: 1;
    SGAA: 1;
    0;
}

switch(FEAT_STATIONS, SELF, _sw_catenary_black, (param["YZ\AA\02", 36] == 0) || (current_year > 1909)){
    1: id_catenary_black;
    id_catenary_black_old;
}

switch(FEAT_STATIONS, SELF, _sw_catenary_ottd, (param["YZ\AA\02", 36] == 0) || (current_year > 1909)){
    1: id_catenary_ottd;
    id_catenary_ottd_old;
}

const none_catenary = FILLED(8);

switch(FEAT_STATIONS, SELF, _sw_catenary_type,
    _sw_catenary() ? param["YZ\AA\02", 5] : none_code
){
    0: _sw_catenary_black();
    1: _sw_catenary_ottd();
    none_catenary;
}

switch(FEAT_STATIONS, SELF, _sw_catenary_info_side,
    hasbit(sw_yard(), pos_front) ? 3 :
    hasbit(sw_yard(), pos_back) ? 4 :
    platform_position_from_end(PLATFORM_SAME_SECTION) == 0 &&
    platform_position_from_start(PLATFORM_SAME_SECTION) % 2 == 0 ? 2 :
    platform_position_from_start(PLATFORM_SAME_SECTION) % 2
){return;}

switch(FEAT_STATIONS, SELF, _sw_catenary_info_normal,
    platform_position_from_end(PLATFORM_SAME_SECTION) == 0 &&
    platform_position_from_start(PLATFORM_SAME_SECTION) % 2 == 0 ? 2 :
    platform_position_from_start(PLATFORM_SAME_SECTION) % 2
){return;}

const none_pylon = FILLED(3);

switch(FEAT_STATIONS, SELF, _sw_pylon_info_even,
    platform_position_from_start(PLATFORM_SAME_SECTION) % 2 == 1 ? none_pylon :
    ((platform_number_from_start(PLATFORM_SAME_SECTION) == 0) << 0) |
    ((platform_number_from_end(PLATFORM_SAME_SECTION) == 0) << 1)
){
    0: 0;
    1: !nearby_tile_is_station(0, -1) || !sw_tile_same_height_left() ? 3 : 1;
    2: 2;
    none_pylon;
}

switch(FEAT_STATIONS, SELF, _sw_pylon_info_odd,
    (
        (platform_position_from_start(PLATFORM_SAME_SECTION) % 2 == 0) ?
            (
                platform_position_from_end(PLATFORM_SAME_SECTION) == 0
                 ?
                (
                    ((platform_number_from_start(PLATFORM_SAME_SECTION) == 0) << 0) |
                    ((platform_number_from_end(PLATFORM_SAME_SECTION) == 0) << 1)
                ) : (
                    none_pylon
                )
            ) : (
                ((platform_number_from_start(PLATFORM_SAME_SECTION) == 0) << 0) |
                ((platform_number_from_end(PLATFORM_SAME_SECTION) == 0) << 1)
            )
    )
){
    0: 0;
    1: !nearby_tile_is_station(0, -1) || !sw_tile_same_height_left() ? 3 : 1;
    2: 2;
    none_pylon;
}

switch(FEAT_STATIONS, SELF, _sw_pylon_type, current_year){
    0..1909: id_pylon_wood;
    1910..2000: id_pylon_old;
    id_pylon_new;
}

const pos_catenary_info_normal = 0;
const pos_catenary_info_side = 2;
const pos_catenary_type = 5;
const pos_catenary_info_normal_size = 2;
const pos_catenary_info_side_size = 3;
const pos_catenary_type_size = 8;
// returns a 13 bit value for pylon placement
// aaaaaaaa bbb cc
switch(FEAT_STATIONS, SELF, sw_catenary,
    _sw_catenary_info_normal() << pos_catenary_info_normal |
    _sw_catenary_info_side() << pos_catenary_info_side |
    _sw_catenary_type() << pos_catenary_type
){return;}

const pos_pylon_info_even = 0;
const pos_pylon_info_odd = 3;
const pos_pylon_type = 6;
const pos_pylon_info_even_size = 3;
const pos_pylon_info_odd_size = 3;
const pos_pylon_type_size = 8;
// returns a 14 bit value for pylon placement
// aaaaaaaa bbb ccc
switch(FEAT_STATIONS, SELF, sw_pylon,
    _sw_pylon_info_even() << pos_pylon_info_even |
    _sw_pylon_info_odd() << pos_pylon_info_odd |
    _sw_pylon_type() << pos_pylon_type
){return;}
